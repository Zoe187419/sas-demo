* S6_7.sas
*
* Using the FREQ statement;

* Roll up the data to the clinic level;   
title1 'Refining REPORT Appearance';
title2 'Using the FREQ Statement';

* For demonstration purposes summarize the data in regions 1 & 10;
proc means data=rptdata.clinics(where=(region in('1','10'))) 
           noprint;
   class region ht;
   var ht;
   output out=clinics(keep=_type_ region ht count mean variance)
          n=count mean=mean var=variance;
   run;

title3 'Region Mean / Variance from PROC MEANS';
proc print data=clinics(where=(_type_=2)) noobs;
   var region count mean variance;
   run;

title3 'Summarized data used by REPORT';
proc print data=clinics(where=(_type_=3)) noobs;
   var region ht count;
   run;

title3 'Mean and Variance Generated by REPORT';
proc report data=clinics nowd;
   column region ht ht=htN ht=htvar;
   define region / group format=$6.;
   define htn    / analysis n    'N';
   define ht     / analysis mean 'Mean';
   define htvar  / analysis var  'Variance';
   freq count;
   run;
 
title3 'Without including the FREQ Statement';
proc report data=clinics nowd;
   column region ht ht=htN ht=htvar;
   define region / group format=$6.;
   define htn    / analysis n    'N';
   define ht     / analysis mean 'Mean';
   define htvar  / analysis var  'Variance';
   run; 
