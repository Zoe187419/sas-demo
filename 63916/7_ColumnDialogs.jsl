// JSL Companion: Applications of the JMP Scripting Language 
// Title: 	7_ColumnDialogs.jsl 
// Version: Windows JMP 9.0.3
// Purpose: Demonstrates scripts for making column dialog boxes and retrieving user input.

//Run the statements below in segments delimited by //----------------- 


bc_dt = Open( "$SAMPLE_DATA/Big Class.JMP" );

col_dlg = Column Dialog(
	_yVar = ColList( "Y, Response" ),
	_xVar = ColList( "X, Treatment" ),
	_grpVar = ColList( "Group Factors" ),
	_wtVar = ColList( "Weight" )
); 

xv = col_dlg["_xVar"];
yv = col_dlg["_yVar"];

Show( col_dlg, col_dlg["_yVar"], col_dlg["_xVar"],
  col_dlg["_grpVar"], col_dlg["_wtVar"], col_dlg["Button"], xv, yv );

If (col_dlg["Button"] == -1, Throw( "User Cancel") );
Remove From( col_dlg , N Items(col_dlg) ); //Remove the last item
Eval List ( col_dlg );

Show( col_dlg, _yVar, _xVar, _grpVar, _wtVar );

//------------------------- Bizarre extremely long text--------------------------------
//Bizarre example of extreme button, for demonstration only

col_dlg = Column Dialog(
	    _lotsaDash = ColList( Repeat("-",1000) ),
	    _xonly = ColList( "X" ));

//not user friendly at all

//---------------- Column Dialog with text and EditNumber constructor ----------------
//--add text and EditNumber control 
col_dg = Column Dialog(
	_yVar = ColList( "Y, Response", MaxCol( 1 ), MinCol( 1 ), DataType( Numeric ) ),
	_xVar = ColList( "X, Treatment", MaxCol( 5 ), MinCol( 1 ), DataType( Numeric ) ),
	_grpVar = ColList( "Group Factors", DataType( Character ) ),
	_wtVar = ColList( "Weight" ),
	HList( "Confidence Interval Alpha", _alpha = EditNumber( .05 ) )
);

//Controls are always placed below the column selection portion of the dialog

//--------------- Column Dialog with additional constructors --------------------------
//The order of the variables is intentionally confusing in this example.
//The response is usually first in JMP dialogs.
dt = Open("$Sample_Data\Snapdragon.jmp" );
mkspace=Repeat(" ",8);
rsp = Column Dialog(
	_trtId = ColList( "X, Treatment", Max Col( 1 ) ),
	_grpId = ColList( "Group Factors" ),
	_respId = ColList( "Y, Response" ),
	_wtId = ColList( "Weight" ),
   Lineup(5,
		Lineup(2, "Alpha", _xalpha = EditNumber( .05 ),
			   "Beta", _xbeta = EditNumber(.10 )  ),
		mkspace,
		V List(       
		"Select interval type. ","Only 1 allowed",
		HList( _xintv = Radio Buttons("Confidence", "Prediction", "Tolerance"))
		),
		mkspace,
		VList( "Select plots.", "No limit.",
		  VList( plot_raw = CheckBox("Raw Data Plot",0), 
		   plot_circ=CheckBox("Comparison Circles",0))
        ) //end select plots
    )// end lineup
);  //end Column Dialog


//Same code/logic as above
If(rsp["Button"]== -1, Caption("Aborting..."); Wait(3); Caption(Remove); Throw());
StatusMsg("Processing next steps");
RemoveFrom(rsp, NItems(rsp));   //Remove the last item "Button(1)"
Eval List(rsp);                      //Assignments are run
Show( rsp, _trtId, _grpId, _respId, _wtId, _xalpha, _xbeta, _xintv, 
    plot_raw, plot_circ);

