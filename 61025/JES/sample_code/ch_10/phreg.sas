
PROC DATASETS LIBRARY=WORK MEMTYPE=DATA GENNUM=ALL KILL; QUIT;

GOPTIONS RESET=ALL;

ODS HTML PATH="&JES.SG\S_10_5" (URL=NONE) BODY="Phreg.html";

DATA LifeTest_2; SET JES.LifeTest;
	Z = 1000/(Temp_C+273.15);
RUN;
PROC SQL NOPRINT;
	CREATE TABLE Temp_Values
	AS SELECT DISTINCT Temp_C, Z FROM LifeTest_2;
QUIT;
DATA Nominal; SET Temp_Values(OBS=1); 
	Temp_C= 90;  Z = 1000/(Temp_C+273.15);	OUTPUT;
RUN;
DATA Temp_Values; SET Nominal Temp_Values;	RUN;


ODS GRAPHICS ON / RESET IMAGENAME="F10_9_";
ODS OUTPUT ParameterEstimates=ParmEst;
PROC PHREG DATA=LifeTest_2 PLOT(OVERLAY CL)=SURVIVAL;
	MODEL TestTime*Censor(-1)= Z ;
	BASELINE OUT=Base SURVIVAL=Surv LOWER=LCL UPPER=UCL  
	COVARIATES=Temp_Values; 
RUN;

ODS GRAPHICS ON / RESET IMAGENAME="F10_10_";
TITLE "Estimated Survival Probability at 90 and 120 Degrees";
PROC SGPLOT Data=Base(WHERE=(Temp_C<=120));
	SERIES Y=Surv X=TestTime / GROUP=Temp_C;
	BAND X=TestTime LOWER=LCL UPPER=UCL /GROUP=Z TRANSPARENCY=.5;
	YAXIS VALUES=(.9 TO 1 BY .02);
RUN;


ODS GRAPHICS OFF;
ODS HTML CLOSE;


